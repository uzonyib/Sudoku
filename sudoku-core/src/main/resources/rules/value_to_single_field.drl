package sudoku;

import sudoku.core.FieldValue;
import sudoku.core.drools.Candidate;
import sudoku.core.drools.DroolsField;

rule "Apply candidate in single field of row"
salience 0
when
	$field : DroolsField(value == null)
	$candidate : Candidate(field == $field)
	not Candidate(this != $candidate, field.rowIndex == $field.rowIndex, value == $candidate.value)
then
	modify($field) {
		setValue($candidate.getValue());
	}
end

rule "Apply candidate in single field of column"
salience 0
when
	$field : DroolsField(value == null)
	$candidate : Candidate(field == $field)
	not Candidate(this != $candidate, field.columnIndex == $field.columnIndex, value == $candidate.value)
then
	modify($field) {
		setValue($candidate.getValue());
	}
end

rule "Apply candidate in single field of block"
salience 0
when
	$field : DroolsField(value == null)
	$candidate : Candidate(field == $field)
	not Candidate(this != $candidate, field.blockIndex == $field.blockIndex, value == $candidate.value)
then
	modify($field) {
		setValue($candidate.getValue());
	}
end